# Copyright 2018 The Hafnium Authors.
#
# Use of this source code is governed by a BSD-style
# license that can be found in the LICENSE file or at
# https://opensource.org/licenses/BSD-3-Clause.

import("//build/image/image.gni")
import("//build/toolchain/platform.gni")
import("//test/hftest/args.gni")

config("config") {
  include_dirs = [ "inc" ]
}

# Tests specific to GICv3.
vm_kernel("gicv3_test_vm") {
  testonly = true
  public_configs = [ ":config" ]

  sources = [
    "busy_secondary.c",
    "gicv3.c",
    "interrupts.c",
    "timer_secondary.c",
  ]

  deps = [
    "//src/arch/aarch64:arch",
    "//src/arch/aarch64/hftest:interrupts",
    "//src/arch/aarch64/hftest:interrupts_gicv3",
    "//test/hftest:hftest_primary_vm",
  ]
}

initrd("gicv3_test") {
  testonly = true

  manifest = "manifest.dts"
  manifest_overlay = hftest_manifest_overlay
  primary_name = "gicv3_test"
  primary_vm = ":gicv3_test_vm"
  secondary_vms = [ [
        "services1",
        "services:gicv3_service_vm1",
      ] ]
}
